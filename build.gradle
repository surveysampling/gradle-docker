buildscript {
  repositories { jcenter() }
}

plugins {
  id 'maven-publish'
  id 'groovy'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
  id "com.github.hierynomus.license" version "0.12.1"
  id 'net.researchgate.release' version '2.3.5'
  id "com.jfrog.artifactory" version "4.0.0"
}

repositories {
    jcenter()
}

apply from: file('gradle/idea.gradle')
apply from: file('gradle/publish.gradle')

task wrapper(type: Wrapper, group: "Build Setup") {
  description 'Installs the Gradle Wrapper into your project.'
  distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleWrapperVersion}-bin.zip"
}

sourceSets {
    integTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        groovy {
            srcDir 'src/integTest/groovy'
        }
        resources {
            srcDir 'src/integTest/resources'
        }
    }
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.google.guava:guava:19.0'

    compile 'com.google.guava:guava:19.0'
    compile 'com.github.docker-java:docker-java:2.1.4'

    testCompile 'junit:junit-dep:4.11'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

task integTest(type: Test) {
    group = 'verification'
    description = 'Runs the integration tests.'
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath

}

// run integration tests after unit tests if both are scheduled for execution
integTest.mustRunAfter test

check.dependsOn(integTest)

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

afterReleaseBuild.dependsOn publish
